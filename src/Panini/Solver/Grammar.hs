{-# LANGUAGE OverloadedLists #-}
module Panini.Solver.Grammar 
  ( GCon(..)
  , grammarConstraints
  , gconKVar
  , solve
  , solveAll  
  ) where

import Algebra.Lattice
import Control.Monad.Extra
import Data.Foldable
import Data.Function
import Data.Generics.Uniplate.Operations qualified as Uniplate
import Data.Hashable
import Data.HashSet (HashSet)
import Data.HashSet qualified as HashSet
import Data.List (partition)
import Data.List qualified as List
import Data.Map.Strict qualified as Map
import Data.Maybe
import GHC.Generics
import Panini.Abstract.AValue
import Panini.Abstract.Semantics
import Panini.Error
import Panini.Monad
import Panini.Panic
import Panini.Pretty
import Panini.Provenance
import Panini.Solver.Assignment
import Panini.Solver.Constraints
import Panini.Syntax
import Prelude

-------------------------------------------------------------------------------

-- TODO: could this go into Constraints.hs ?

-- | A /grammar constraint/ is any constraint of the form @‚àÄs:ùïä. Œ∫(s) ‚áí c@.
-- Here, @Œ∫@ is known as a /grammar variable/ and @c@ as a /grammar consequent/.
-- The string variable @s@, applied parameter to @Œ∫@, is a free variable in @c@.
data GCon = GCon Name KVar Con
  deriving stock (Eq, Show, Read, Generic)

instance Hashable GCon

instance Pretty GCon where
  pretty (GCon x k c) = pretty $ CAll x TString (PAppK k [Var x]) c

-- | Returns all grammar constraints within the given constraint.
grammarConstraints :: Con -> HashSet GCon
grammarConstraints c0 = HashSet.fromList
  [GCon x k c | CAll x TString (PAppK k [Var y]) c <- Uniplate.universe c0
              , y == x
  ]

gconKVar :: GCon -> KVar
gconKVar (GCon _ k _) = k

-------------------------------------------------------------------------------

-- | Solve a set of grammar constraints, returning the combined solution.
-- 
-- The current approach orders constraints by Œ∫ variable name and tries to solve
-- them sequentially, applying intermediate solutions on the way.
solveAll :: HashSet GCon -> Pan Assignment
solveAll = foldM solve1 mempty
         . List.sortBy (compare `on` gconKVar)
         . HashSet.toList
  where
    solve1 s (GCon x k c) = do
      logMessage $ "Solve grammar variable" <+> pretty k
      g <- solve $ GCon x k $ apply s c
      logMessage $ "Found grammar assignment for" <+> pretty k
      logData g
      return $ Map.union g s

-- | Solve a grammar constraint @‚àÄs:ùïä. Œ∫(s) ‚áí c@, returning a solution for @Œ∫@.
solve :: GCon -> Pan Assignment

-- Trick to solve (simple) recursions: eliminate recursive Œ∫ applications by
-- replacing them simply with true, then try to solve normally. If the
-- recursions did not actually affect the grammar, the solution will validate
-- the VC, which still includes the recursive applications. Otherwise, the
-- recursion must have encoded some information that was lost by this simple
-- elimination and the VC will be judged invalid.
solve (GCon s k c) | k `elem` kvars c = do
  logData c
  logMessage $ "Eliminate recursive grammar variable" <+> pretty k
  let c' = apply [(k,PTrue)] c
  solve (GCon s k c')

solve (GCon s k c) = do
  logData c
  c' <- rewrite c
  g <- joins <$> mapM ((meets <$>) . mapM (abstractVarString s)) (unDNF c')  
  p <- PRel <$> concretizeVar s (EAbs $ AString g)

  -- IMPORTANT: we need to substitute the free string variable s in the
  -- grammar solution with the generic Œ∫ parameter, so that later on we can
  -- apply without problems
  let p' = subst (Var $ head $ kparams k) s p

  return $ Map.singleton k p'

abstractVarString :: Name -> Rel -> Pan AString
abstractVarString x r = abstractVar x TString r >>= \case
  EAbs (AString s) -> return s
  a -> panic $ "expected abstract string instead of" <+> pretty a

-------------------------------------------------------------------------------

rewrite :: Con -> Pan (DNF Rel)
rewrite = \case
  CHead p      -> return $ toDNF p
  CAnd c1 c2   -> liftA2 (‚àß) (rewrite c1) (rewrite c2)
  CAll x b p c -> case c of
    CAll x2 b2 p2 c2 -> varElimDNF x b =<< rewrite (CAll x2 b2 (p ‚àß p2) c2)
    CHead q          -> varElimDNF x b $ toDNF $ p ‚àß q
    CAnd c1 c2 -> (joins <$>) . forM (unDNF $ toDNF p) $ \p' -> do
        c1' <- varElimDNF x b =<< meet (DNF [p']) <$> rewrite c1
        c2' <- varElimDNF x b =<< meet (DNF [p']) <$> rewrite c2
        return $ c1' ‚üë c2'

(‚üë) :: DNF a -> DNF a -> DNF a
DNF [] ‚üë DNF [] = DNF []
p      ‚üë DNF [] = p
DNF [] ‚üë q      = q
p      ‚üë q      = p ‚àß q

-------------------------------------------------------------------------------

newtype DNF a = DNF { unDNF :: [[a]] }

instance MeetSemilattice (DNF a) where
  DNF ps ‚àß DNF qs = DNF [p ++ q | p <- ps, q <- qs]

instance BoundedMeetSemilattice (DNF a) where
  top = DNF [[]]

instance JoinSemilattice (DNF a) where
  DNF [[]] ‚à® _        = DNF [[]]
  _        ‚à® DNF [[]] = DNF [[]]
  DNF ps   ‚à® DNF qs   = DNF (ps ++ qs)

instance BoundedJoinSemilattice (DNF a) where
  bot = DNF []

toDNF :: Pred -> DNF Rel
toDNF p0 = DNF $ unwrapDNF $ flip Uniplate.rewrite p0 $ \case
  PAnd xs
    | PFalse `elem` xs -> Just PFalse
    | PTrue `elem` xs -> Just $ PAnd $ List.filter (/= PTrue) xs
    | any isPAnd xs ->
      let (ys, zs) = partition isPAnd xs
      in Just $ PAnd $ mconcat [y | PAnd y <- ys] ++ zs
    | any isPOr xs -> case partition isPOr xs of
        (POr ys : yys, zs) -> Just $ POr $ [PAnd $ y : (yys ++ zs) | y <- ys]
        _                  -> impossible
    | or [PRel (inverse r) `elem` xs | PRel r <- xs] -> Just PFalse
    | HashSet.size (HashSet.fromList xs) < length xs -> Just $ PAnd $ List.nub xs  -- TODO

  POr xs
    | PTrue `elem` xs -> Just PTrue
    | PFalse `elem` xs -> Just $ POr $ List.filter (/= PFalse) xs
    | any isPOr xs ->
      let (ys, zs) = partition isPOr xs
      in Just $ POr $ mconcat [y | POr y <- ys] ++ zs

  PNot (POr xs) -> Just $ PAnd $ map PNot xs
  PNot (PAnd xs) -> Just $ POr $ map PNot xs
  PNot (PRel r) -> Just $ PRel $ inverse r

  PImpl a b -> Just $ POr [PNot a, b]
  PIff a b -> Just $ POr [PAnd [PNot a, PNot b], PAnd [a, b]]

  _ -> Nothing

unwrapDNF :: Pred -> [[Rel]]
unwrapDNF = \case
  POr  xs -> unOr xs
  PAnd ys -> [unAnd ys]
  PTrue   -> [[]]
  PFalse  -> []
  PRel r  -> [[r]]
  p -> panic $ "expected POr/PAnd/PTrue/PFalse/PRel instead of" <+> pretty p
  where
    unAnd ys
      | all isPRel ys = [y | PRel y <- ys]
      | otherwise = panic $ "expected all PRel instead of" <+> pretty ys
    unOr xs
      | all isPAnd xs = [unAnd ys | PAnd ys <- xs]
      | otherwise = panic $ "expected all PAnd instead of" <+> pretty xs

-------------------------------------------------------------------------------

varElimDNF :: Name -> Base -> DNF Rel -> Pan (DNF Rel)
varElimDNF x b ps = DNF <$> mapMaybeM (varElim x b) (unDNF ps)

-- TODO: update submission
-- | Algorithm 3 in OOPSLA'23 submission.
varElim :: Name -> Base -> [Rel] -> Pan (Maybe [Rel])
varElim _ TUnit ps = return $ Just ps  -- TODO
varElim x b ps = do
  let bTop = topExpr b

  let xÃÇs‚ÇÄ = Map.singleton [x] bTop
  let pvs = [(p,vÃÑ) | p <- ps, let vÃÑ = freeVars p, x `elem` vÃÑ]
  let refine xÃÇs (p,vÃÑ) = do
        let xÃÇ‚ÇÄ = fromMaybe bTop $ Map.lookup vÃÑ xÃÇs
        xÃÇ‚ÇÅ <- abstractVar x b p
        case xÃÇ‚ÇÄ ‚àß? xÃÇ‚ÇÅ of
          Just xÃÇ -> return $ Map.insert vÃÑ xÃÇ xÃÇs
          Nothing -> throwError $ MeetImpossible xÃÇ‚ÇÄ xÃÇ‚ÇÅ (getPV x)
  xÃÇs <- foldM refine xÃÇs‚ÇÄ pvs

  case fromJust $ Map.lookup [x] xÃÇs of
    EAbs a | containsBot a -> return Nothing
    xÃÇSelf -> do
      let xÃÇs' = filter (([x] /=) . fst) $ Map.assocs xÃÇs
      -- TODO: pick "smallest" meet
      let (vÃÑ‚Çò,xÃÇ‚Çò) = if null xÃÇs' then ([x], xÃÇSelf) else head xÃÇs'
      let qs = map (substExpr xÃÇ‚Çò x) $ filter ((vÃÑ‚Çò /=) . freeVars) ps
      return $ Just qs

-- TODO: can this be done using the new Subable class?
substExpr :: Expr -> Name -> Rel -> Rel
substExpr xÃÇ x =  Uniplate.transformBi $ \case
  EVal (Var y) | y == x -> xÃÇ
  e                     -> e

-------------------------------------------------------------------------------

isPOr :: Pred -> Bool
isPOr (POr _) = True
isPOr _       = False

isPAnd :: Pred -> Bool
isPAnd (PAnd _) = True
isPAnd _        = False

isPRel :: Pred -> Bool
isPRel (PRel _) = True
isPRel _        = False
